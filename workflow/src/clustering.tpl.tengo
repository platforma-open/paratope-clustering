self := import("@platforma-sdk/workflow-tengo:tpl")
ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")

processResultsSw := assets.importSoftware("@platforma-open/milaboratories.paratope-clustering.software:process-results")
mmseqsSw := assets.importSoftware("@platforma-open/soedinglab.software-mmseqs2:main")
createEmptyFilesSw := assets.importSoftware("@platforma-open/milaboratories.paratope-clustering.software:create-empty-files")

self.validateInputs({
	"__options__,closed": "",
	emptyOrNot: "any",
	fasta: "any",
	paratopeSequences: "any",
	cloneTable: "any",
	isSingleCell: "boolean",
	identity: "number",
	similarityType: "string",
	coverageThreshold: "number",
	coverageMode: "number",
	numSequences: "number",
	"mem,?": "number",
	"cpu,?": "number"
})

self.defineOutputs("abundances", "clusterToSeq", "cloneToCluster", "abundancesPerCluster", "distanceToCentroid", "clusterRadius", "clusterToSeqTop", "clusterRadiusTop", "abundancesTop", "paratopeSequences", "mmseqs", "mmseqsOutput", "isEmpty")

self.body(func(inputs) {
	mmseqs := {}
	mmseqsOutput := {}

	if string(inputs.emptyOrNot.getData()) == "empty" {
		numSequences := inputs.numSequences

		emptyFilesBuilder := exec.builder().
			software(createEmptyFilesSw).
			mem("1GiB").
			cpu(1).
			arg("--num-sequences").arg(string(numSequences))

		if inputs.isSingleCell {
			emptyFilesBuilder = emptyFilesBuilder.arg("--is-single-cell")
		}

		emptyFiles := emptyFilesBuilder.
			saveFile("abundances.tsv").
			saveFile("cluster-to-seq.tsv").
			saveFile("clone-to-cluster.tsv").
			saveFile("abundances-per-cluster.tsv").
			saveFile("distance_to_centroid.tsv").
			saveFile("cluster-radius.tsv").
			saveFile("cluster-to-seq-top.tsv").
			saveFile("cluster-radius-top.tsv").
			saveFile("abundances-top.tsv").
			saveFile("paratope-sequences.tsv").
			run()

		return {
			abundances: emptyFiles.getFile("abundances.tsv"),
			clusterToSeq: emptyFiles.getFile("cluster-to-seq.tsv"),
			cloneToCluster: emptyFiles.getFile("clone-to-cluster.tsv"),
			abundancesPerCluster: emptyFiles.getFile("abundances-per-cluster.tsv"),
			distanceToCentroid: emptyFiles.getFile("distance_to_centroid.tsv"),
			clusterRadius: emptyFiles.getFile("cluster-radius.tsv"),
			clusterToSeqTop: emptyFiles.getFile("cluster-to-seq-top.tsv"),
			clusterRadiusTop: emptyFiles.getFile("cluster-radius-top.tsv"),
			abundancesTop: emptyFiles.getFile("abundances-top.tsv"),
			paratopeSequences: emptyFiles.getFile("paratope-sequences.tsv"),
			mmseqs: mmseqs,
			mmseqsOutput: mmseqsOutput,
			isEmpty: true
		}
	} else {
		// Step 1: Run MMseqs2 clustering on paratope FASTA
		memLimit := "{int(ceil(system.ram.gb * 0.8))}" + "G"

		mem := "32GiB"
		cpu := 16

		if !is_undefined(inputs.mem) {
			mem = string(inputs.mem) + "GiB"
		}
		if !is_undefined(inputs.cpu) {
			cpu = inputs.cpu
		}

		mmseqs := exec.builder().
			software(mmseqsSw).
			mem(mem).
			cpu(cpu).
			printErrStreamToStdout().
			arg("easy-cluster").
			arg("input.fasta").
			arg("result").
			arg("tmp").
			arg("--split-memory-limit").argWithVar(memLimit).
			arg("--threads").argWithVar("{system.cpu}").
			arg("--min-seq-id").arg(string(inputs.identity)).
			arg("-c").arg(string(inputs.coverageThreshold)).
			arg("--cov-mode").arg(string(inputs.coverageMode)).
			arg("--similarity-type").arg(inputs.similarityType == "sequence-identity" ? "2" : "1").
			addFile("input.fasta", inputs.fasta).
			saveFile("result_cluster.tsv").
			saveStdoutStream().
			run()

		clusters := mmseqs.getFile("result_cluster.tsv")
		mmseqsOutput := mmseqs.getStdoutStream()

		// Step 2: Process results
		result := exec.builder().
			software(processResultsSw).
			mem("32GiB").
			cpu(8).
			addFile("clusters.tsv", clusters).
			addFile("cloneTable.tsv", inputs.cloneTable).
			addFile("paratopeSequences.tsv", inputs.paratopeSequences).
			saveFile("abundances.tsv").
			saveFile("cluster-to-seq.tsv").
			saveFile("clone-to-cluster.tsv").
			saveFile("abundances-per-cluster.tsv").
			saveFile("distance_to_centroid.tsv").
			saveFile("cluster-radius.tsv").
			saveFile("cluster-to-seq-top.tsv").
			saveFile("cluster-radius-top.tsv").
			saveFile("abundances-top.tsv").
			saveFile("paratope-sequences.tsv").
			run()

		return {
			abundances: result.getFile("abundances.tsv"),
			clusterToSeq: result.getFile("cluster-to-seq.tsv"),
			cloneToCluster: result.getFile("clone-to-cluster.tsv"),
			abundancesPerCluster: result.getFile("abundances-per-cluster.tsv"),
			distanceToCentroid: result.getFile("distance_to_centroid.tsv"),
			clusterRadius: result.getFile("cluster-radius.tsv"),
			clusterToSeqTop: result.getFile("cluster-to-seq-top.tsv"),
			clusterRadiusTop: result.getFile("cluster-radius-top.tsv"),
			abundancesTop: result.getFile("abundances-top.tsv"),
			paratopeSequences: result.getFile("paratope-sequences.tsv"),
			mmseqs: clusters,
			mmseqsOutput: mmseqsOutput,
			isEmpty: false
		}
	}
})
